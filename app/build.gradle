
group = 'com.hfyd.atlas.demo'
version = getEnvValue("versionName", "1.0.0");
def apVersion = getEnvValue("apVersion", "1.0.0");

apply plugin: 'com.taobao.atlas.application'
apply plugin: 'com.taobao.atlas'

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.atlas.hfyd"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName version
        vectorDrawables.useSupportLibrary = true
        //通过增加判断逻辑，打出不同类型的定制包
        if (project.hasProperty("beta")) {
            buildConfigField "boolean", "API_ENV", "false"
        }else{
            buildConfigField "boolean", "API_ENV", "false"
        }
        multiDexEnabled = true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            ndk {
                abiFilters "x86","armeabi"
            }
        }

        packagingOptions {
            exclude 'META-INF/rxjava.properties'
        }
    }
}

atlas {
    //声明为awb 即bundle工程
    atlasEnabled true
    instantAppEnabled true

    patchConfigs {
        debug {
            //打动态部署 patch 包
            createTPatch true
        }
    }

    tBuildConfig {
        //自启动bundle配置
        //远程bundle
        outOfApkBundles = ['activebundle']
        classInject false
        aaptConstantId true
    }

    manifestOptions {
        addAtlasProxyComponents true
    }

    buildTypes {
        debug {
            if (apVersion) {
                baseApDependency "com.hfyd.atlas.demo:AP-debug:${apVersion}@ap"
                patchConfig patchConfigs.debug
            }
        }
    }
}

String getEnvValue(key, defValue) {
    def val = System.getProperty(key);
    if (null != val) {
        return val;
    }
    val = System.getenv(key);
    if (null != val) {
        return val;
    }
    return defValue;
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

/*    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-vector-drawable:25.1.0'
    compile 'com.android.support:support-v4:25.1.0'*/
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    compile 'com.alibaba:fastjson:1.1.45.android@jar'


    compile rootProject.ext.lib_atlas_core
    compile rootProject.ext.lib_atlas_update

    compile project(':core_base')
    compile project(':core_cache')
    compile project(':core_image')
    compile project(':core_logger')
    compile project(':core_network')
    compile project(':core_parse')
    compile project(':core_widget')

    bundleCompile project(':framework')

    bundleCompile project(':newsbundle')
    bundleCompile project(':activebundle')
}


apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    repositories {
        mavenLocal()
    }
}

repositories {
    mavenLocal()
    jcenter()
}


publishing {
    publications {
        maven(MavenPublication) {
            artifact "${project.buildDir}/outputs/apk/debug/${project.name}-debug.ap"
            artifactId "AP-debug"
        }
    }
}